// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM4K.hdl

/**
 * Memory of 4K registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM4K {
    IN in[16], load, address[12];
    OUT out[16];

    PARTS:

    // address has 12 bits. Most significant 3 bits (address[9..11]) tells us
    // which RAM512 we'll operate on and the least significant 9 bits
    // (address[0..8]) tells us which register in that particular RAM512 we'll
    // use. Check the comments in RAM64 for the rationale.

    // Select which RAM512 to load to.
    DMux8Way (in=load,
              sel=address[9..11],
              a=selectRam1,
              b=selectRam2,
              c=selectRam3,
              d=selectRam4,
              e=selectRam5,
              f=selectRam6,
              g=selectRam7,
              h=selectRam8);

    RAM512 (in=in, load=selectRam1, address=address[0..8], out=ram1Out);
    RAM512 (in=in, load=selectRam2, address=address[0..8], out=ram2Out);
    RAM512 (in=in, load=selectRam3, address=address[0..8], out=ram3Out);
    RAM512 (in=in, load=selectRam4, address=address[0..8], out=ram4Out);
    RAM512 (in=in, load=selectRam5, address=address[0..8], out=ram5Out);
    RAM512 (in=in, load=selectRam6, address=address[0..8], out=ram6Out);
    RAM512 (in=in, load=selectRam7, address=address[0..8], out=ram7Out);
    RAM512 (in=in, load=selectRam8, address=address[0..8], out=ram8Out);

    // Pipe only the output of the RAM512 defined by the relevant "address" bits
    // to out.
    Mux8Way16 (a=ram1Out,
               b=ram2Out,
               c=ram3Out,
               d=ram4Out,
               e=ram5Out,
               f=ram6Out,
               g=ram7Out,
               h=ram8Out,
               sel=address[9..11],
               out=out);
}
